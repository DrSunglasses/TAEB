#!/usr/bin/perl
use strict;
use warnings;
use FindBin '$Bin';
use YAML::Syck;

my %options;

# XXX: I tried using Module::Install::Can::can_run but it always returned true..
if (`which nethack`) {
    my $dialog = join ' ', split /\n/, q{
        /Applications/CocoaDialog.app/Contents/MacOS/CocoaDialog
        msgbox
        --button1 Local
        --button2 Telnet
        --button3 Cancel
        --text 'How would you like TAEB to play NetHack?'
        --informative-text 'The Telnet interface requires an internet connection.'
        --title 'TAEB: Interface'
        --icon globe
    };
    my $interface_id = `$dialog`;

    exit if $interface_id == 3; # cancel

    $options{interface} = $interface_id == 1 ? 'Local' : 'Telnet';
}
else {
    $options{interface} = 'Telnet';
}

# Ask about server, username, passwordj
if ($options{interface} eq 'Telnet') {
    # I would use standard-inputbox here except they use 'Ok' instead of 'OK' :(
    my $server_dialog = join ' ', split /\n/, q{
        /Applications/CocoaDialog.app/Contents/MacOS/CocoaDialog
        inputbox
        --button1 OK
        --button2 Cancel
        --text nethack.alt.org
        --informative-text 'What server would you like TAEB to use?'
        --title 'TAEB: Server'
    };
    my $server_response = `$server_dialog`;
    my ($server_code, $server) = split /\n/, $server_response;
    exit if $server_code == 2 || $server !~ /\S/;

    $options{interface_options}{Telnet}{server} = $server;

    # ask for name
    my $name_dialog = join ' ', split /\n/, q{
        /Applications/CocoaDialog.app/Contents/MacOS/CocoaDialog
        inputbox
        --button1 "New Account"
        --button2 "Existing Account"
        --button3 Cancel
        --informative-text 'What username would you like TAEB to use?'
        --title 'TAEB: Name'
    };
    my $name_response = `$name_dialog`;
    my ($name_code, $name) = split /\n/, $name_response;
    exit if $name_code == 3 || $name !~ /\S/;

    my $register = $name_code == 1;
    $options{interface_options}{Telnet}{register} = 1
        if $register;

    $options{interface_options}{Telnet}{account} = $name;

    # Maybe I'm being paranoid, oh well
    $name =~ s/'//g;

    # ask for password
    my $password_dialog = join ' ', split /\n/, qq{
        /Applications/CocoaDialog.app/Contents/MacOS/CocoaDialog
        inputbox
        --button1 OK
        --button2 Cancel
        --informative-text 'What is the password for $name?'
        --title 'TAEB: Password'
        --no-show
    };
    my $password_response = `$password_dialog`;
    my ($password_code, $password) = split /\n/, $password_response;
    exit if $password_code == 2 || $password !~ /\S/;

    $options{interface_options}{Telnet}{password} = $password;

    my $email = $ENV{EMAIL} || '';
    # paranoia again
    $email =~ s/'//g;

    # ask for email address
    if ($register) {
        my $email_dialog = join ' ', split /\n/, qq{
            /Applications/CocoaDialog.app/Contents/MacOS/CocoaDialog
            inputbox
            --button1 OK
            --button2 Cancel
            --informative-text 'What is your email address?'
            --title 'TAEB: Email'
            --text '$email'
        };
        my $email_response = `$email_dialog`;
        my ($email_code, $email) = split /\n/, $email_response;
        exit if $email_code == 2 || $email !~ /\S/;

        $options{interface_options}{Telnet}{email} = $email;
    }
}

my $config = Dump(\%options);
exec $^X, "$Bin/taeb", "--config=$config";

